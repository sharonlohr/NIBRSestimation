/* Select probability sample of non-NIBRS LEAs in Arizona */

OPTIONS NOFMTERR;

/* PATH should contain location of computational folder */
%LET PATH = C:;
%PUT &PATH;

FILENAME PHOENIX "&PATH.\Data from AZ cities\PHX_count.csv";
FILENAME TUCSON "&PATH.\Data from AZ cities\Tucson_count.csv";
FILENAME TEMPE "&PATH.\Data from AZ cities\Tempe_count.csv";

LIBNAME NIBRS "&PATH";

DATA AGENCY2021;
   SET NIBRS.AGENCY2021;
   IF NUMSTATE = 2;
RUN;

PROC CONTENTS DATA=AGENCY2021;
 TITLE 'CONTENTS AGENCY2021';
RUN;

DATA OFFENSEH2021;
   SET NIBRS.OFFENSEH2021;
   IF NUMSTATE = 2;
RUN;

/* CALCULATE TOTAL POPULATION */

PROC MEANS DATA=AGENCY2021 N SUM;
   VAR CURRENT_POPULATION;
   TITLE 'TOTAL POPULATION FROM AZ AGENCIES';
   OUTPUT OUT = AZ_TOTAL_POP SUM = AZ_TOTAL_POP;
RUN;

DATA AZ_TOTAL_POP;
   SET AZ_TOTAL_POP;
   CALL SYMPUT('AZ_TOTAL_POP',AZ_TOTAL_POP);
RUN;

%PUT &AZ_TOTAL_POP;

/* LOOK AT AGENCIES COVERED BY ANOTHER ORI */

PROC FREQ DATA=AGENCY2021;
  TABLES COVERED_BY_ORI/LIST MISSING;
   TITLE 'AGENCIES COVERED BY ANOTHER ORI';
RUN;

PROC PRINT DATA=AGENCY2021;
   WHERE COVERED_BY_ORI NE ' ';
   TITLE 'AGENCIES COVERED BY ANOTHER ORI';
RUN;

/* IN DATA SET COVEREDBY, ORI IS SET EQUAL TO THE AGENCY COVERING THE CRIME REPORTS.
   SUM THE POPULATIONS OF ALL LEAS COVERED BY AN AGENCY */ 

DATA COVEREDBY (KEEP = ORI POPCOPY);
   SET AGENCY2021;
   IF COVERED_BY_ORI NE ' '  THEN ORI = COVERED_BY_ORI;
   POPCOPY = CURRENT_POPULATION;
RUN;

PROC SORT DATA=COVEREDBY;
   BY ORI;

PROC MEANS DATA=COVEREDBY SUM N;
   BY ORI;
   VAR POPCOPY;
   OUTPUT OUT = COVERED_SUM SUM = POPSUM;
RUN;

PROC PRINT DATA=COVERED_SUM; RUN;

PROC SORT DATA=COVERED_SUM;
   BY ORI;
PROC SORT DATA=AGENCY2021;
   BY ORI;

/* MERGE THE DATA. IN COVERED, ORI IS THE COVERING AGENCY AND ITS 
   CURRENT_POPULATION IS SET TO THE SUM OF ALL AGENCIES IT COVERS */

DATA COVERED (DROP = POPSUM COVERED_BY_ORI);
   MERGE COVERED_SUM (IN = A) AGENCY2021 (IN = B);
   BY ORI;
   IF B;
   CURRENT_POPULATION = MAX(CURRENT_POPULATION,POPSUM);
   IF COVERED_BY_ORI = ' '; /* ELIMINATE RECORDS OF AGENCIES COVERED BY OTHERS */
RUN;

PROC SORT DATA=COVERED;
   BY POP_GROUP DESCENDING CURRENT_POPULATION DESCENDING MONTHS_REPORTED;
RUN;

PROC PRINT DATA=COVERED;
   VAR ORI CITY_NAME POP_GROUP AGENCY_INDICATOR CORE_CITY NIBRS_FLAG CURRENT_POPULATION MSA_CODE MONTHS_REPORTED ANY_NIBRS THREE_NIBRS PARTICIPATE; 
   TITLE '2021 Arizona agencies';
RUN;

/* CHECK PARTICIPATION VARIABLE */

PROC PRINT DATA=COVERED;
   VAR CITY_NAME NAME NIBRS_FLAG ANY_NIBRS THREE_NIBRS MONTHS_REPORTED PARTICIPATE CURRENT_POPULATION;
   TITLE 'CHECK PARTICIPATION VARIABLE';
RUN;

PROC FREQ DATA=COVERED;
   TABLES NIBRS_FLAG * PARTICIPATE * MONTHS_REPORTED / LIST MISSING;
RUN;

PROC PRINT DATA=COVERED;
   WHERE NIBRS_FLAG = 'A' AND PARTICIPATE = 0;
   VAR CITY_NAME NAME NIBRS_FLAG ANY_NIBRS THREE_NIBRS MONTHS_REPORTED PARTICIPATE CURRENT_POPULATION;
   TITLE 'AGENCIES WITH NIBRS_FLAG A BUT NO MONTHS REPORTED';
RUN;

PROC PRINT DATA=COVERED;
   WHERE PARTICIPATE = 3;
   VAR ORI CITY_NAME NAME POP_GROUP CURRENT_POPULATION AGENCY_INDICATOR CORE_CITY NIBRS_FLAG CURRENT_POPULATION MONTHS_REPORTED ANY_NIBRS THREE_NIBRS PARTICIPATE; 
   TITLE '2021 Arizona agencies with full NIBRS participation';
RUN;

PROC PRINT DATA=COVERED;
   WHERE PARTICIPATE < 3;
   VAR ORI CITY_NAME NAME POP_GROUP CURRENT_POPULATION NIBRS_FLAG CURRENT_POPULATION MSA_CODE MONTHS_REPORTED PARTICIPATE AGCYTYPE AGENCY_INDICATOR CORE_CITY ; 
   TITLE '2021 Arizona agencies with PARTIAL OR NO NIBRS participation';
RUN;

PROC SORT DATA=COVERED;
BY ORI;

/* CALCULATE POPULATION COVERAGE */

PROC MEANS DATA=COVERED N SUM;
   CLASS PARTICIPATE;
   VAR CURRENT_POPULATION;
   OUTPUT OUT = POPCOVER SUM = POPSUM;
RUN;

DATA POPCOVER;
   SET POPCOVER;
   POPCOVER = POPSUM/&AZ_TOTAL_POP;
RUN;

PROC PRINT DATA=POPCOVER;
   TITLE 'PERCENT OF AZ POPULATION COVERED BY NIBRS BY PARTICIPATION CATEGORY';
RUN;

/* CREATE STRATA FOR SAMPLE */

/*  STRATA DEFINITIONS
   1  LEAS WITH 12 MONTHS OF NIBRS REPORTING, N = 77
   2  LARGE LEAS: PHOENIX, TUCSON, MCSO, GLENDALE, TEMPE N = 5
   3  CITIES FROM 10,000 TO 49,999 (POP_GROUP = 4,5)
   4  CITIES LESS THAN 9,999 AND ZERO-POPULATION AGENCIES  (POP_GROUP = 6,7)
   5  NON-MSA COUNTIES (POP_GROUP = 8B,8C,8D), N = 5
*/

DATA AGENCY2021POP;
   SET COVERED;
   IF MONTHS_REPORTED = 12 THEN STRATUM = 1;
   ELSE IF POP_GROUP IN ("1A","1B","1C","9A","2") THEN STRATUM = 2;
   ELSE IF POP_GROUP IN ("4","5") THEN STRATUM = 3;
   ELSE IF POP_GROUP IN ("6","7") THEN STRATUM = 4;
   ELSE IF POP_GROUP IN ("8B","8C", "8D") THEN STRATUM = 5;
   POP = CURRENT_POPULATION;
   IF POP = 0 THEN POP = 2500; /* DEFINE POP TO BE NONZERO FOR ZERO-POP AGENCIES SO THEY HAVE POSITIVE MEASURE OF SIZE */
   IF ORI IN ("AZ0079700","AZ0109700") THEN POP = 10000; /* INCREASE POP FOR ASU, UA PDS */
RUN;

PROC SORT DATA=AGENCY2021POP;
   BY STRATUM;

PROC MEANS DATA=AGENCY2021POP N MEAN STDDEV VAR;
   BY STRATUM;
   VAR POP;
   OUTPUT OUT = STRATVAR VAR = _VAR_ N = _TOTAL_;
   TITLE 'STRATUM COUNTS AND VARIANCE OF CURRENT_POPULATION';
RUN;

PROC PRINT DATA=STRATVAR;
RUN;

/* LOOK AT OPTIMAL ALLOCATION FOR RESPONSE VARIABLE CURRENT_POPULATION */

PROC SURVEYSELECT DATA=AGENCY2021POP METHOD = SRS N = 10 OUT = NEYMANALLOC;
   WHERE STRATUM > 2;
   STRATA STRATUM/ ALLOC = NEYMAN VAR = STRATVAR NOSAMPLE;
RUN;

PROC PRINT DATA=NEYMANALLOC; 
RUN;

/* SELECT SAMPLE OF AGENCIES, STRATIFIED BY POPGROUP */

DATA STRATSIZE;
   INPUT STRATUM _NSIZE_;
   DATALINES;
3 2
4 4
5 3
;

DATA STRATSIZE (KEEP = STRATUM _NSIZE_ _TOTAL_);
   MERGE STRATSIZE STRATVAR;
   BY STRATUM;
   IF STRATUM IN (1,2) THEN _NSIZE_ = _TOTAL_;
RUN;

PROC PRINT DATA=STRATSIZE;
RUN;


TITLE 'SAMPLE OF LEAS FROM AZ'; 
PROC SURVEYSELECT DATA=AGENCY2021POP METHOD = SRS SAMPSIZE=STRATSIZE OUT = AZSAMPLE STATS SEED = 928624;
   STRATA STRATUM ;
RUN;

PROC PRINT DATA=AZSAMPLE;
  WHERE STRATUM > 2;
  VAR ORI STRATUM CITY_NAME NAME POP_GROUP CURRENT_POPULATION MONTHS_REPORTED PARTICIPATE SAMPLINGWEIGHT 
   JAN_ACT FEB_ACT MAR_ACT APR_ACT MAY_ACT JUN_ACT JUL_ACT AUG_ACT SEP_ACT OCT_ACT NOV_ACT DEC_ACT;  
RUN;

/* CHECK SAMPLE SIZES */

PROC FREQ DATA=AZSAMPLE;
   TABLES STRATUM;
RUN;

/* OBTAIN COUNTS FOR PHOENIX, TUCSON, TEMPE FROM DATA FILES */

DATA PHOENIX (KEEP = ORI CITY_NAME UCR_OFFENSE COUNT);
  LENGTH ORI $ 9 DESCRIPTION $ 25 SUB_DESCRIPTION $ 50 ;
  INFILE PHOENIX DSD DELIMITER = ',' FIRSTOBS = 2;
  INPUT ORI	$ CITY_Name $	UCR_OFFENSE	$ SUB_TOC $	SUB_TOC_CODE$	MONTH	COUNT Type $ Description $	Sub_Description $;

DATA TUCSON (KEEP = ORI CITY_NAME UCR_OFFENSE COUNT);
  LENGTH ORI $ 9 UCRDescription $ 25  ;
  INFILE TUCSON DSD DELIMITER = ',' FIRSTOBS = 2;
  INPUT UCR_OFFENSE	$ UCRDescription $	OFFENSE_NUM	MONTH	COUNT	ORI $	CITY_NAME $;
RUN;

DATA TEMPE (KEEP = ORI CITY_NAME UCR_OFFENSE COUNT);
  LENGTH ORI $ 9 ;
  INFILE TEMPE DSD DELIMITER = ',' FIRSTOBS = 2;
  INPUT UCR_OFFENSE $ HIER_OFF $ OFFENSE_NUM	MONTH	COUNT	ORI $	CITY_NAME $;
RUN;

DATA AZCITY;
  LENGTH HIER_OFF $ 8;
  SET PHOENIX TUCSON TEMPE;
  IF UCR_OFFENSE IN ("09A","09B") THEN DO;
      HIER_OFF = "MURDER";
	  OFFENSE_NUM = 1;
   END;
   ELSE IF UCR_OFFENSE IN ("11","11A","11B","11C") THEN DO;
      HIER_OFF = "RAPE";
	  OFFENSE_NUM = 2;
   END;   
   ELSE IF UCR_OFFENSE IN ("120") THEN DO;
       HIER_OFF = "ROBBERY";
	   OFFENSE_NUM = 3;
   END;
   ELSE IF UCR_OFFENSE IN ("13A") THEN DO;
       HIER_OFF = "AGASSLT";
	   OFFENSE_NUM = 4;
   END;
   ELSE IF UCR_OFFENSE IN ("220") THEN DO;
       HIER_OFF = "BURGLARY";
	   OFFENSE_NUM = 5;
   END;
   ELSE IF UCR_OFFENSE IN ("240") THEN DO;
       HIER_OFF = "MVTHEFT";
	   OFFENSE_NUM = 6;
   END;
   ELSE IF UCR_OFFENSE IN ("23","23A","23B","23C","23D","23E","23F","23G","23H") THEN DO;
       HIER_OFF = "LARCENY";
	   OFFENSE_NUM = 7;
   END;
   IF OFFENSE_NUM = . THEN DELETE;
RUN;

PROC PRINT DATA=AZCITY; 
  TITLE 'DATA FROM AZ CITIES';
RUN;

PROC SORT DATA=AZCITY;
   BY ORI OFFENSE_NUM HIER_OFF;
RUN;

/* SUM THE CRIME COUNTS FOR CITIES OVER MONTHS OF THE YEAR */

PROC MEANS DATA=AZCITY NOPRINT;
   BY ORI OFFENSE_NUM HIER_OFF;
   VAR COUNT;
   OUTPUT OUT = AZCITY_COUNT SUM = CRIME_COUNT;
RUN;

PROC PRINT DATA=AZCITY_COUNT;
RUN;

PROC TRANSPOSE DATA=AZCITY_COUNT OUT = AZCITY_COUNT_WIDE;
   BY ORI ;
   ID HIER_OFF;
   VAR CRIME_COUNT;
RUN;

PROC PRINT DATA=AZCITY_COUNT_WIDE;
RUN;

/* GET THE CRIME COUNTS FOR THE AGENCIES IN STRATUM 1  */

DATA ORI_PARTICIPATE (KEEP = ORI PARTICIPATE MONTHS_REPORTED);
   SET COVERED;
RUN;

PROC SORT DATA=ORI_PARTICIPATE;
   BY ORI;

PROC SORT DATA=OFFENSEH2021;
   BY ORI;

DATA OFFENSEH2021_COUNT;
   MERGE ORI_PARTICIPATE OFFENSEH2021;
   BY ORI;

PROC SORT DATA=OFFENSEH2021_COUNT;
   BY ORI;

PROC FREQ DATA=OFFENSEH2021_COUNT NOPRINT;
   BY ORI;
   WHERE PARTICIPATE = 3;
   TABLES HIER_OFF / OUT = FULL_NIBRS_REPORTERS;
   TITLE 'CRIME COUNTS FOR FULL NIBRS REPORTERS (HIERARCHICAL RULE)';
RUN;

PROC TRANSPOSE DATA=FULL_NIBRS_REPORTERS OUT = STRATUM1_WIDE;
   BY ORI;
   ID HIER_OFF;
   VAR COUNT;
RUN;

DATA STRATUM1_WIDE (KEEP = ORI MURDER RAPE ROBBERY AGASSLT BURGLARY MVTHEFT LARCENY);
   ARRAY CRIME_TYPE{7} MURDER RAPE ROBBERY AGASSLT BURGLARY MVTHEFT LARCENY;
   SET STRATUM1_WIDE;
   DO J = 1 TO 7;
      IF CRIME_TYPE{J} = . THEN CRIME_TYPE{J} = 0;
   END;
RUN;

PROC PRINT DATA=STRATUM1_WIDE;
   TITLE 'STRATUM 1 LEAS';
RUN;

PROC MEANS DATA=STRATUM1_WIDE SUM;
   VAR MURDER RAPE ROBBERY AGASSLT BURGLARY MVTHEFT LARCENY;
   TITLE 'CRIME TOTALS FOR 12-MONTH NIBRS REPORTERS';
RUN;

/* GET THE CRIME COUNTS FOR PARTIAL NIBRS REPORTERS  */

PROC SORT DATA=OFFENSEH2021_COUNT;
   BY ORI;

PROC FREQ DATA=OFFENSEH2021_COUNT NOPRINT;
   BY ORI;
   WHERE PARTICIPATE IN (1,2);
   TABLES HIER_OFF / OUT = PARTIAL_NIBRS_REPORTERS;
   TITLE 'CRIME COUNTS FOR PARTIAL NIBRS REPORTERS (HIERARCHICAL RULE)';
RUN;

PROC PRINT DATA=PARTIAL_NIBRS_REPORTERS;
RUN;

PROC SORT DATA=PARTIAL_NIBRS_REPORTERS;
   BY ORI;
RUN;

PROC TRANSPOSE DATA=PARTIAL_NIBRS_REPORTERS OUT = PARTIAL_REPORTERS_WIDE PREFIX = PARTIAL_;
   BY ORI;
   ID HIER_OFF;
   VAR COUNT;
RUN;

DATA PARTIAL_REPORTERS_WIDE (KEEP = ORI PARTIAL_MURDER PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT 
         PARTIAL_BURGLARY PARTIAL_MVTHEFT PARTIAL_LARCENY);
   ARRAY CRIME_TYPE{7} PARTIAL_MURDER PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT 
         PARTIAL_BURGLARY PARTIAL_MVTHEFT PARTIAL_LARCENY;
   SET PARTIAL_REPORTERS_WIDE;
   DO J = 1 TO 7;
      IF CRIME_TYPE{J} = . THEN CRIME_TYPE{J} = 0;
   END;
RUN;

PROC PRINT DATA=PARTIAL_REPORTERS_WIDE;
RUN;

PROC MEANS DATA=PARTIAL_REPORTERS_WIDE SUM;
    VAR PARTIAL_MURDER PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT 
         PARTIAL_BURGLARY PARTIAL_MVTHEFT PARTIAL_LARCENY;
	TITLE 'CRIME TOTALS FOR 1-11 month NIBRS REPORTERS';
RUN;

PROC SORT DATA=STRATUM1_WIDE;
   BY ORI;
   
PROC SORT DATA=PARTIAL_REPORTERS_WIDE;
   BY ORI;

PROC SORT DATA=AZCITY_COUNT_WIDE;
   BY ORI;
   
PROC SORT DATA=AZSAMPLE;
   BY ORI;

DATA AZSAMPLE_COLLECT (KEEP = ORI STRATUM CITY_NAME NAME POP_GROUP CURRENT_POPULATION 
  MONTHS_REPORTED PARTICIPATE LEA_WT MURDER RAPE ROBBERY AGASSLT BURGLARY MVTHEFT LARCENY
  PARTIAL_MURDER PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT PARTIAL_BURGLARY
  PARTIAL_MVTHEFT PARTIAL_LARCENY);
   MERGE AZSAMPLE AZCITY_COUNT_WIDE STRATUM1_WIDE PARTIAL_REPORTERS_WIDE;
   BY ORI;
   LEA_WT = SAMPLINGWEIGHT;
   IF STRATUM = . THEN DELETE;
RUN;

PROC SORT DATA=AZSAMPLE_COLLECT;
   BY STRATUM DESCENDING CURRENT_POPULATION;
RUN;

PROC CONTENTS DATA=AZSAMPLE_COLLECT; RUN;

PROC PRINT DATA=AZSAMPLE_COLLECT;
   TITLE 'SAMPLE OF AZ LEAS';
RUN;


/* EXPORT TO CSV FILE SO DATA CAN BE COLLECTED */

PROC EXPORT DATA= WORK.AZSAMPLE_COLLECT 
            OUTFILE= "&PATH.\AZsample_to_collect.csv" 
            DBMS=CSV REPLACE;
     PUTNAMES=YES;
RUN;

/* SAVE STRATSIZE FILE TO SAS DATA SET FOR USE IN COMPUTING CRIME ESTIMATES.
   STRATSIZE CONTAINS THE TOTAL NUMBER OF LEAS IN EACH STRATUM FOR 
   CALCULATING FINITE POPULATION CORRECTIONS */

DATA NIBRS.STRATSIZE;
   SET STRATSIZE;
RUN;
