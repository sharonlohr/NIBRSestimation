/* IMPORTS FILES WITH DEMOGRAPHIC INFORMATION FROM AMERICAN COMMUNITY SURVEY */
/* WRITES TO SAS LIBRARY FILES
   ACS_VARS     DEMOGRAPHIC INFO FOR ALL PLACES IN AZ, CA, TX, NM, NV 
   ACS_DONOR    DEMOGRAPHIC INFO FOR CITIES IN NIBRS WITH POP > 500000 IN SOUTHWEST */

OPTIONS NOFMTERR;
/* PUT WORKING DIRECTORY IN NEXT LINE*/
%LET PATH = C:;
%PUT &PATH;
%LET PATH2 = &PATH.\Demographics of donor cities;
%PUT &PATH2;

LIBNAME NIBRS "&PATH";

%INCLUDE "&PATH.\NIBRS macros.sas";


/* Gives information on race alone without ethnicity.  */
PROC IMPORT OUT= WORK.RACE 
            DATAFILE= "&PATH2.\ACSDT1Y2021.B02001-2025-06-11RACE.xlsx" 
            DBMS=EXCEL REPLACE;
     RANGE="Transpose$"; 
     GETNAMES=YES;
     MIXED=NO;
     SCANTEXT=YES;
     USEDATE=YES;
     SCANTIME=YES;
RUN;

proc sql noprint;
select name into :VARLIST_R separated by " "
from dictionary.columns
where LIBNAME = "WORK"
and MEMNAME = "RACE"
and type = 'num';
select CATS("PCT_",name) into :PCTVARLIST_R separated by " "
from dictionary.columns
where LIBNAME = "WORK"
and MEMNAME = "RACE"
and type = 'num';
quit;

%PUT &VARLIST_R;
%PUT &PCTVARLIST_R;

DATA RACE_PCT (KEEP = PLACENAME &VARLIST_R &PCTVARLIST_R);
   LENGTH PLACENAME $ 10;
   ARRAY COUNT_VAR{*} &VARLIST_R;
   ARRAY PCT_VAR{*} &PCTVARLIST_R;
   SET RACE;
   PLACENAME = SUBSTR(PLACE,1,10);
   IF SUBSTR(PLACE,1,4) = "Mesa" then PLACENAME = "Mesa";
   DO J = 1 TO DIM(COUNT_VAR);
      IF TOTAL > 0 THEN PCT_VAR{J} = 100*COUNT_VAR{J}/TOTAL;
   END;
RUN;

PROC PRINT DATA=RACE_PCT (OBS=20);
   VAR PLACENAME &VARLIST_R &PCTVARLIST_R;
   TITLE 'RACE ALONE INFO ';
RUN;

/* FILE WITH AGE, RACE AND ETHNICITY COMBINED */

PROC IMPORT OUT= WORK.AGE_RACE_ETH 
            DATAFILE= "&PATH2.\ACSDP1Y2021.DP05-2025-06-11AGE_RACE_ETH.xlsx" 
            DBMS=EXCEL REPLACE;
     RANGE="Transpose$"; 
     GETNAMES=YES;
     MIXED=NO;
     SCANTEXT=YES;
     USEDATE=YES;
     SCANTIME=YES;
RUN;


proc sql noprint;
select name into :VARLIST_A separated by " "
from dictionary.columns
where LIBNAME = "WORK"
and MEMNAME = "AGE_RACE_ETH"
and type = 'num';
select CATS("PCT_",name) into :PCTVARLIST_A separated by " "
from dictionary.columns
where LIBNAME = "WORK"
and MEMNAME = "AGE_RACE_ETH"
and type = 'num';
quit;

%PUT &VARLIST_A;
%PUT &PCTVARLIST_A;

DATA AGE_RACE_ETH_PCT (KEEP = PLACE PLACENAME IN_NIBRS COUNTY_IND CITY_IND FOUR_CITIES STATE &VARLIST_A &PCTVARLIST_A);
   LENGTH PLACENAME $ 10;
   ARRAY COUNT_VAR{*} &VARLIST_A;
   ARRAY PCT_VAR{*} &PCTVARLIST_A;
   SET AGE_RACE_ETH;
   PLACENAME = SUBSTR(PLACE,1,10);
   IF SUBSTR(PLACE,1,4) = "Mesa" then PLACENAME = "Mesa";
   DO J = 1 TO DIM(COUNT_VAR);
      IF TOTAL > 0 THEN PCT_VAR{J} = 100*COUNT_VAR{J}/TOTAL;
   END;
   IF INDEX(PLACE,"Arizona") > 0 THEN STATE = "AZ";
   ELSE IF INDEX(PLACE,"California") > 0 THEN STATE = "CA";
   ELSE IF INDEX(PLACE,"New Mexico") > 0 THEN STATE = "NM";
   ELSE IF INDEX(PLACE,"Nevada") > 0 THEN STATE = "NV";
   ELSE IF INDEX(PLACE,"Colorado") > 0 THEN STATE = "CO";
   ELSE IF INDEX(PLACE,"Texas") > 0 THEN STATE = "TX";
   IF INDEX(PLACE,"County") > 0 THEN COUNTY_IND = 1; ELSE COUNTY_IND = 0;
   IF INDEX(PLACE,"City") > 0 OR INDEX(PLACE,"city") > 0 OR INDEX(PLACE,"town") > 0 THEN CITY_IND = 1; ELSE CITY_IND = 0;
   IF SUBSTR(PLACE,1,5) IN ("Phoen","Tucso","Glend","Tempe") THEN FOUR_CITIES = 1;
RUN;

PROC PRINT DATA=AGE_RACE_ETH_PCT (OBS = 20); WHERE STATE="AZ"; RUN;

PROC FREQ DATA=AGE_RACE_ETH_PCT;
   TABLES STATE/LIST MISSING;
   TITLE 'AGE/RACE/ETH INFO BY STATE';
RUN;

PROC PRINT DATA=AGE_RACE_ETH_PCT ;
   WHERE STATE = "AZ" AND SUBSTR(PLACE,1,5) IN ("Arizo","Phoen","Tucso","Glend","Tempe");
   VAR PLACENAME IN_NIBRS TOTAL PCT_HISP PCT_NH_W NH_W PCT_NH_B NH_B PCT_NH_AIAN NH_AIAN PCT_NH_A NH_A      PCT_NH_OTHER_RACE PCT_NH_TWO_PLUS_RACE 
   PCT_AGE_00_TO_05 PCT_AGE_05_TO_09 PCT_AGE_10_TO_14 PCT_AGE_15_TO_19 PCT_AGE_20_TO_24 PCT_AGE_25_TO_34 ;
TITLE 'AGE/RACE/ETH INFO FOR LARGE AZ NIBRS NON-REPORTERS';
RUN;

PROC MEANS DATA=AGE_RACE_ETH_PCT SUM;
   WHERE STATE="AZ" AND SUBSTR(PLACE,1,5) IN ("Phoen","Tucso","Glend","Tempe");
   VAR TOTAL HISP NH_W NH_B NH_AIAN NH_A AGE_10_TO_14 AGE_15_TO_19 AGE_20_TO_24 AGE_25_TO_34;
   OUTPUT OUT = SUM4CITIES SUM = TOTAL HISP NH_W NH_B NH_AIAN NH_A AGE_10_TO_14 AGE_15_TO_19 AGE_20_TO_24 AGE_25_TO_34;
RUN;

PROC PRINT DATA=SUM4CITIES; 
TITLE 'SUM OF DEMOGRAPHIC COUNTS FOR PHOENIX, TUCSON, GLENDALE, TEMPE';
RUN;

/* LOOK AT SUM OF DEMO VARIABLES FOR NIBRS CITIES */

PROC MEANS DATA=AGE_RACE_ETH_PCT SUM;
   WHERE STATE="AZ" AND IN_NIBRS = 1 AND CITY_IND = 1;
   VAR TOTAL HISP NH_W NH_B NH_AIAN NH_A AGE_10_TO_14 AGE_15_TO_19 AGE_20_TO_24 AGE_25_TO_34;
   OUTPUT OUT = SUM_NIBRS_CITIES SUM = TOTAL HISP NH_W NH_B NH_AIAN NH_A AGE_10_TO_14 AGE_15_TO_19 AGE_20_TO_24 AGE_25_TO_34;
TITLE 'SUM OF DEMO INFO FOR NIBRS CITIES IN AZ';
RUN;

PROC PRINT DATA=SUM_NIBRS_CITIES;
RUN;


PROC IMPORT OUT= WORK.SELPOPEST 
            DATAFILE= "&PATH2.\ACSSPP1Y2021.S0201-2025-06-11SELPOPESTS.xlsx" 
            DBMS=EXCEL REPLACE;
     RANGE="Transpose$"; 
     GETNAMES=YES;
     MIXED=NO;
     SCANTEXT=YES;
     USEDATE=YES;
     SCANTIME=YES;
RUN;

PROC CONTENTS DATA=SELPOPEST; RUN;
PROC PRINT DATA=SELPOPEST; RUN;


DATA SELPOPEST (DROP = PLACE );
   LENGTH PLACENAME $ 10 ;
   SET SELPOPEST;
   PLACENAME = SUBSTR(PLACE,1,10);
   IF SUBSTR(PLACE,1,4) = "Mesa" THEN PLACENAME = "Mesa";
   ONERACE = PCT_ONE_RACE*TOTAL_POP/100;
   TWORACE = PCT_TWO_RACE*TOTAL_POP/100;
   THREERACE = PCT_3_RACE*TOTAL_POP/100;	
   MARRIED = PCT_MARRIED*POP_15_AND_OVER/100;	
   DIVORCED = PCT_DIVORCED*POP_15_AND_OVER/100;	
   NEVER_MARRIED = PCT_NEVER_MARRIED*POP_15_AND_OVER/100;	
   HS_AND_OVER = ROUND(PCT_HS_AND_OVER*POP_25_AND_OVER/100);
   BACHELOR_AND_HIGHER = ROUND(PCT_BACHELOR_AND_HIGHER*POP_25_AND_OVER/100);
   POVERTY_ALL = ROUND(PCT_POVERTY_ALL*TOTAL_POP/100);
   POVERTY_UNDER_18 = ROUND(PCT_POVERTY_UNDER_18*AGE_UNDER_18/100);
   NO_HEALTH_INS = ROUND(PCT_NO_HEALTH_INS*TOT_CIV_NONINST_POP/100);
   PCT_AGE_UNDER_18 = 100*AGE_UNDER_18/TOTAL_POP;
   PCT_AGE_18_TO_34 = 100*AGE_18_TO_34/TOTAL_POP;
   PCT_AGE_35_TO_64 = 100*AGE_35_TO_64/TOTAL_POP;
RUN;

PROC PRINT DATA=SELPOPEST;
RUN;

PROC SORT DATA=SELPOPEST;
   BY PLACENAME;
PROC SORT DATA= AGE_RACE_ETH_PCT;
   BY PLACENAME;
RUN;
PROC SORT DATA= RACE_PCT;
   BY PLACENAME;
RUN;

DATA ACS_VARS;
   MERGE SELPOPEST AGE_RACE_ETH_PCT (DROP = TOTAL PCT_TOTAL);
   BY PLACENAME;
RUN;

PROC PRINT DATA=ACS_VARS;
   VAR PLACENAME PLACE ORI TOTAL_POP AGE_18_TO_34 AGE_20_TO_24 AGE_25_TO_34 BACHELOR_AND_HIGHER POVERTY_ALL  NH_B  NH_AIAN ;
RUN;

PROC CONTENTS DATA=SELPOPEST;
PROC CONTENTS DATA=AGE_RACE_ETH_PCT;
PROC CONTENTS DATA=ACS_VARS;
RUN;

%LET MATCHVARS = HISP NH_W  NH_B  NH_AIAN  BACHELOR_AND_HIGHER AGE_18_TO_34 POVERTY_ALL;
%PUT &MATCHVARS;

PROC PRINT DATA=ACS_VARS (OBS = 10);
   VAR ORI PLACENAME &MATCHVARS;
   TITLE 'ACS_VARS';
RUN;


DATA ACS_DONORS;
   SET ACS_VARS;
   IF IN_NIBRS = 1 AND TOTAL_POP > 450000 AND CITY_IND = 1;
RUN;

PROC PRINT DATA=ACS_DONORS;
   VAR ORI PLACENAME PLACE &MATCHVARS;
   TITLE 'ACS_DONORS';
RUN;

PROC PRINT DATA=ACS_DONORS; VAR ORI PLACENAME; RUN;

DATA ACS_DONORS_VARCOUNT (DROP = PCT:);
   SET ACS_DONORS;
RUN;

PROC CONTENTS DATA=ACS_DONORS_VARCOUNT;
   TITLE 'ACS_DONORS_VARCOUNT: LIST VARIABLES';
RUN;

PROC PRINT DATA=ACS_DONORS_VARCOUNT; RUN;


DATA NIBRS.ACS_DONORS;
   SET ACS_DONORS;
RUN;


DATA NIBRS.ACS_VARS;
   SET ACS_VARS;
RUN;


