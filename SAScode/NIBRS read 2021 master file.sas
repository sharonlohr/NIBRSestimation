/* Read NIBRS Master Data File for AZ and imputation donor LEAs.
   Define hierarchical offense structure for calculating SRS crime rates and
   for imputing incidents. */

/* Uses macros READ_NIBRS, WRITE_NIBRS */

/* PATH is location of computational folder; NIBRS is location of NIBRS Master Files */
%LET PATH = C:;
%PUT &PATH;
%LET DATAPATH = C:;
%PUT &DATAPATH;

LIBNAME NIBRS "&PATH";

%INCLUDE "&PATH.\NIBRS macros.sas";

%LET DONOR_CITY_SET = (
"NM0010100", /* Albuquerque */ 
"TX2270100", /* Austin */
"TXDPD0000", /* Dallas */ 
"CODPD0000", /* Denver */ 
"TX0710200", /* El Paso */
"TX2201200", /* Fort Worth */
"CA0100500", /* Fresno */
"TXHPD0000", /* Houston */
"NV0020100", /* Las Vegas */
"AZ0071700", /* Mesa */
"TXSPD0000", /* San Antonio */ 
"CA0371100"  /* San Diego */
);
%PUT &DONOR_CITY_SET;

FILENAME NIBR2021 "&DATAPATH.\nibrs-2021\2021_NIBRS_NATIONAL_MASTER_FILE.txt" ;


%READ_NIBRS(YEAR = 2021, FNAME = NIBR2021, STATENUM = 2, AGCYLIST = &DONOR_CITY_SET);

/* COMBINE BATCH2021 WITH CROSSWALK FILE DA35158P1 */
/* READ CROSSWALK FILE */

PROC CIMPORT 
  FILE = "&PATH.\Data from AZ cities\ICPSR_35158-V2\ICPSR_35158\DS0001\35158-0001-Data.stc"
  LIB = WORK;
RUN;

PROC CONTENTS DATA=DA35158P1;
  TITLE 'CONTENTS DA35158P1';
RUN;

PROC SORT DATA=DA35158P1;
   BY ORI9;

DATA BATCH2021;
   SET BATCH2021;
   ORI9 = ORI;
RUN;

PROC SORT DATA=BATCH2021;
   BY ORI9;
RUN;

PROC SORT DATA=DA35158P1;
   BY ORI9;
RUN;

/* PRINT AGENCIES IN BATCH2021 BUT NOT CROSSWALK FILE AND VICE VERSA */

DATA CROSSWALK;
   MERGE BATCH2021 (IN = A) DA35158P1 (IN = B);
   BY ORI9;
   IF A AND NOT B THEN MISMATCH = 1;
   IF B AND NOT A THEN MISMATCH = 2;
RUN;

PROC PRINT DATA=CROSSWALK;
   WHERE MISMATCH IN (1) AND (NUMSTATE = 2 OR ADDRESS_STATE='AZ');
   VAR ORI ORI9 MISMATCH NAME FIPS_COUNTY_1;
   TITLE 'AGENCIES IN NIBRS MASTER FILE BUT NOT IN CROSSWALK';
RUN;

/* MERGE THE "BH" RECORDS WITH THE CROSSWALK FILE SO WE HAVE THE LEA NAMES.
   DEFINE THE NAMES FOR THE ORIS WITH NO MATCH IN THE CROSSWALK FILE.
   THIS INFORMATION CAME FROM THE FBI CRIME DATA EXPLORER, WHICH LISTS THE ORIS FOR REPORTING AGENCIES */

DATA AGENCY2021 (KEEP = YEAR NUMSTATE ORI NAME DATE_ORI_ADDED DATE_ORI_NIBRS 
         CITY_NAME STATE_ABB POP_GROUP COUNTRY_DIVISION 
         REGION AGENCY_INDICATOR CORE_CITY COVERED_BY_ORI 
         NIBRS_FLAG INDICATOR_01_06_12 MONTHS_REPORTED PARTICIPATE ANY_NIBRS THREE_NIBRS
         CURRENT_POPULATION UCR_COUNTY_CODE MSA_CODE FIPS_COUNTY_1 FCOUNTY
         CURRENT_POPULATION_2 UCR_COUNTY_CODE_2 MSA_CODE_2 FIPS_COUNTY_2
         CURRENT_POPULATION_3 UCR_COUNTY_CODE_3 MSA_CODE_3 FIPS_COUNTY_3
         JAN_ACT FEB_ACT MAR_ACT APR_ACT 
         MAY_ACT JUN_ACT JUL_ACT AUG_ACT 
         SEP_ACT OCT_ACT NOV_ACT DEC_ACT 
	     AGCYTYPE);
   MERGE BATCH2021 (IN = A) DA35158P1;
   BY ORI9;
   IF NUMSTATE = 2 AND A; /* KEEP ONLY AZ RECORDS */
   IF ORI = "AZ007990X" THEN DO;
        NAME = "LITCHFIELD PARK POLICE DEPARTMENT";
        CITY_NAME = "LITCHFIELD PARK"; 
   END;
   IF ORI = "AZDI08900" THEN DO;
        NAME = "HOPI RESOURCE ENFORCEMENT AGENCY";
        CITY_NAME = "KEAMS CANYON";
   END;
RUN;

PROC PRINT DATA=AGENCY2021;
   TITLE 'LIST OF AZ LEAS';
RUN;

/* WRITE FILES TO SAS LIBRARY */

/* WRITE AZ AGENCY LIST WITH LEA NAMES */
DATA NIBRS.AGENCY2021;
   SET AGENCY2021;
RUN;

/* WRITE BATCH, ADMIN, OFFENSEM, OFFENSEH, PROPERTY, VICTIM, OFFENDER FILES FOR EACH YEAR */

DATA NIBRS.BATCH2021;
   SET BATCH2021;
RUN;

DATA NIBRS.ADMIN2021;
   SET ADMIN2021;
RUN;

DATA NIBRS.OFFENSEM2021;
   SET OFFENSEM2021;
RUN;

DATA NIBRS.OFFENSEH2021;
   SET OFFENSEH2021;
RUN;

DATA NIBRS.VICTIM2021;
   SET VICTIM2021;

DATA NIBRS.OFFENDER2021;
   SET OFFENDER2021;
RUN;

DATA NIBRS.PROPERTY2021;
   SET PROPERTY2021;
RUN;
