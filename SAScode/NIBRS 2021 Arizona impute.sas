/* Impute NIBRS records for sampled responding and partially responding AZ agencies */

OPTIONS NOFMTERR;
/* Substitute locations of computational folder in PATH and NIBRS Master files in DATAPATH
%LET PATH = C:;
%PUT &PATH;
%LET DATAPATH = C:;
%PUT &DATAPATH;

LIBNAME NIBRS "&PATH";

%INCLUDE "&PATH.\NIBRS macros.sas";

FILENAME AZSAMPLE "&PATH.\AZcounts.csv";

FILENAME NIBR2022 "&DATAPATH.\nibrs-2022\2022_NIBRS_NATIONAL_MASTER_FILE.txt" ;
FILENAME NIBR2023 "&DATAPATH.\nibrs-2023\2023_NIBRS_NATIONAL_MASTER_FILE.txt" ;

/* Set random number seed for first imputation */
%LET SEED1           = 1372932 ;  /* STARTING SEED FOR IMPUTATIONS */

/* IMPORT ARIZONA SAMPLE, PUT PARTIAL-, NON-NIBRS REPORTERS IN IMPUTE_SAMPLE  */

DATA AZSAMPLE;
   LENGTH ORI $ 9 CITY_NAME $ 30 NAME $ 50;
   INFILE AZSAMPLE DSD DELIMITER = ',' FIRSTOBS = 2;
   INPUT STRATUM ORI $ CITY_NAME $ POP_GROUP $	CURRENT_POPULATION	MONTHS_REPORTED	PARTICIPATE	NAME $
     MURDER RAPE ROBBERY AGASSLT BURGLARY MVTHEFT LARCENY	
     PARTIAL_MURDER	PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT PARTIAL_BURGLARY PARTIAL_MVTHEFT PARTIAL_LARCENY 
     LEA_WT;
RUN;

PROC PRINT DATA=AZSAMPLE;
   TITLE 'ARIZONA SAMPLE';
RUN;


/* LIST THE ORIS IN THE SAMPLE TO BE IMPUTED THAT HAVE PARTIAL NIBRS REPORTING */
PROC SQL NOPRINT;  
  SELECT ORI INTO :PARTIAL_ORI_LIST SEPARATED BY ' ' FROM AZSAMPLE WHERE PARTICIPATE IN (1,2);
  SELECT QUOTE(ORI,'"') INTO :PARTIAL_QUOTE_ORI SEPARATED BY ', ' FROM AZSAMPLE WHERE PARTICIPATE IN (1,2);
QUIT;

%LET PARTIAL_LEAS = (&PARTIAL_QUOTE_ORI.);
%PUT &PARTIAL_ORI_LIST;
%PUT &PARTIAL_QUOTE_ORI;
%PUT &PARTIAL_LEAS;

/* REMOVE FULL NIBRS REPORTERS AND LITCHFIELD PARK FROM THE SET TO BE IMPUTED.
   LATER IN PROGRAM, LITCHFIELD PARK IS PUT INTO THE NIBRS BASE FILE.
   ITS WEIGHT IS CHANGED (SEE PAPER) AND NO IMPUTATION IS NEEDED */
DATA IMPUTE_SAMPLE;
   SET AZSAMPLE;
   WHERE STRATUM > 1 AND ORI NE "AZ007990X";
RUN;

PROC PRINT DATA=IMPUTE_SAMPLE;
   TITLE 'ARIZONA SAMPLE MEMBERS TO BE IMPUTED ';
RUN;

%LET DSID     = %SYSFUNC(OPEN(WORK.IMPUTE_SAMPLE));
%LET NOBS     = %SYSFUNC(ATTRN(&DSID,NLOBS));
%LET RC       = %SYSFUNC(CLOSE(&DSID));
%PUT &NOBS;



/* LIST THE ORIS IN THE SAMPLE TO BE IMPUTED */
PROC SQL NOPRINT;  
  SELECT ORI INTO :ORI_LIST SEPARATED BY ' ' FROM IMPUTE_SAMPLE;
  SELECT CATS("MOS_",ORI) INTO :MOS_ORI_LIST SEPARATED BY ' '  FROM IMPUTE_SAMPLE;
  SELECT QUOTE(ORI,'"') INTO :QUOTE_ORI SEPARATED BY ', ' FROM IMPUTE_SAMPLE;
QUIT;

%LET SAMPLED_LEAS = (&QUOTE_ORI.);
%PUT &MOS_ORI_LIST;
%PUT &ORI_LIST;
%PUT &QUOTE_ORI;
%PUT &SAMPLED_LEAS;


/* IMPORT HIERARCHICAL INCIDENT DATA WITH DONOR RECORDS, ALONG WITH VICTIM, OFFENDER, PROPERTY FILES */

/* IMPORT THE 2021 FILES */

DATA AGENCY2021;
   SET NIBRS.AGENCY2021;
DATA OFFENSEH2021;
   SET NIBRS.OFFENSEH2021;
DATA OFFENSEM2021;
   SET NIBRS.OFFENSEM2021;
DATA VICTIM2021;
   SET NIBRS.VICTIM2021;
DATA OFFENDER2021;
   SET NIBRS.OFFENDER2021;
DATA PROPERTY2021;
   SET NIBRS.PROPERTY2021;
RUN;

   
/* IMPORT DONOR RECORDS FROM 2022 AND 2023 NIBRS MASTER FILES.
   USE THESE FOR IMPUTATION OF AGENCIES IN 2021 SAMPLE THAT JOINED NIBRS IN 2022 OR 2023.
   SET STATENUM = 900 SO NO STATES ARE SELECTED */

%READ_NIBRS(YEAR = 2022, FNAME = NIBR2022, STATENUM = 900, AGCYLIST = &SAMPLED_LEAS);
%READ_NIBRS(YEAR = 2023, FNAME = NIBR2023, STATENUM = 900, AGCYLIST = &SAMPLED_LEAS);

PROC PRINT DATA=BATCH2022;
   VAR ORI CITY_NAME POP_GROUP DATE_ORI_NIBRS NIBRS_FLAG MONTHS_REPORTED CURRENT_POPULATION
   JAN_ACT FEB_ACT MAR_ACT APR_ACT MAY_ACT JUN_ACT JUL_ACT AUG_ACT SEP_ACT OCT_ACT NOV_ACT DEC_ACT ;
   TITLE '2022 NIBRS INFO FOR LEAS IN 2021 SAMPLE';
RUN;


PROC PRINT DATA=BATCH2023;
   WHERE MONTHS_REPORTED > 0;
   VAR ORI CITY_NAME POP_GROUP DATE_ORI_NIBRS NIBRS_FLAG MONTHS_REPORTED CURRENT_POPULATION
   JAN_ACT FEB_ACT MAR_ACT APR_ACT MAY_ACT JUN_ACT JUL_ACT AUG_ACT SEP_ACT OCT_ACT NOV_ACT DEC_ACT ;
   TITLE '2023 NIBRS INFO FOR LEAS IN 2021 SAMPLE';
RUN;


DATA DONOR_22_23  (DROP = J ORI_ARRAY1-ORI_ARRAY&NOBS);
   ARRAY MOS{&NOBS} &MOS_ORI_LIST;
   ARRAY ORI_ARRAY{&NOBS} $9 (&QUOTE_ORI);
   SET OFFENSEH2022 OFFENSEH2023;
   IF ORI IN &SAMPLED_LEAS;
   DO J = 1 TO &NOBS;
       MOS{J} = 0;
       IF ORI = ORI_ARRAY{J} THEN MOS{J} = 1;
   END;
   IF YEAR = 2022 THEN INCIDENT_MONTH = ROUND((INCIDENT_DATE - 20220000)/100);
   IF YEAR = 2023 THEN INCIDENT_MONTH = ROUND((INCIDENT_DATE - 20230000)/100);
   IF ORI = "AZ0071300" AND INCIDENT_MONTH LE 6 THEN MOS_AZ0071300 = 2; 
      /* INCREASE MOS FOR FIRST SIX MONTHS FOR GLENDALE BECAUSE IT STARTED NIBRS IN JULY 2022 */
RUN;


PROC FREQ DATA=DONOR_22_23;
   TABLES (&MOS_ORI_LIST)*ORI;
   TITLE 'CHECK DEFINITION OF MOS IN 2022, 2023 DONORS';
RUN;


PROC FREQ DATA=DONOR_22_23;
   TABLES ORI*HIER_OFF;
   TITLE 'COUNT OFFENSES IN DONORS FROM 2022, 2023';
RUN;

/* SET UP SELECTION PROBABILITIES FOR DONOR DATA FROM 2021
   MOS FOR PHX, TUCSON SET TO 0 FOR NOW --- THESE ARE DETERMINED DIFFERENTLY FOR EACH IMPUTATION */

PROC SORT DATA=AGENCY2021;
   BY ORI;
PROC SORT DATA=OFFENSEH2021;
   BY ORI;

DATA DONOR_2021 (DROP = J);
   ARRAY MOS{&NOBS} &MOS_ORI_LIST;
   MERGE OFFENSEH2021 (IN = A) AGENCY2021 (KEEP = ORI MONTHS_REPORTED PARTICIPATE POP_GROUP);
   BY ORI;
   IF A;
   INCIDENT_MONTH = ROUND((INCIDENT_DATE - 20210000)/100);
   DO J = 1 TO &NOBS;
       MOS{J} = 0;
   END;
   IF POP_GROUP IN ("9A","9B","9C","9D","9E") THEN MOS_AZ0070000 = 1; /* MCSO */
   IF POP_GROUP IN ("8A","8B","8C","8D","8E","9A","9B","9C","9D","9E") THEN DO;
       MOS_AZ0050000 = 1; /* GRAHAM COUNTY SO */
	   IF HIER_OFF IN ("ROBBERY") THEN MOS_AZ0090000 = 0.01; /* NAVAJO COUNTY SO, GIVE SMALL MOS SINCE 2022-23 HAVE NO ROBBERIES */
   END; 
   IF POP_GROUP IN ("4","5") THEN DO;
       MOS_AZ0110700 = 1; /*  FLORENCE */
       MOS_AZ0130300 = 1; /*  COTTONWOOD */
   END;
   IF POP_GROUP IN ("6","7") THEN DO;
       MOS_AZ0130500 = 1; /*  JEROME */
	   MOS_AZ0090900 = 1; /*  WINSLOW */
   END;
RUN;

DATA NIBRS_DONOR;
   SET DONOR_2021 DONOR_22_23;
RUN;

PROC FREQ DATA=NIBRS_DONOR;
   TABLES (&MOS_ORI_LIST)*HIER_OFF;
   TITLE 'CHECK MOS FOR EACH LEA IN SAMPLE ';
RUN;

/* PERFORM IMPUTATIONS. FIRST, OBTAIN BASE FILE FROM THE FULL AND PARTIAL NIBRS REPORTERS. */

DATA AZ_STRATUM1 (KEEP = ORI STRATUM LEA_WT CURRENT_POPULATION);
   SET AZSAMPLE;
   IF STRATUM = 1 OR ORI IN &PARTIAL_LEAS;
RUN;

PROC SORT DATA=AZ_STRATUM1;
   BY ORI;

PROC SORT DATA=OFFENSEH2021;
   BY ORI;

DATA NIBRS_BASE_FILE;
   MERGE AZ_STRATUM1 (IN = A) OFFENSEH2021 (IN = B);
   BY ORI;
   IF A;
   IMPUTE_FLAG = 0;
RUN;

PROC PRINT DATA=NIBRS_BASE_FILE (OBS = 20);
   WHERE ORI = "AZ007990X";
   TITLE 'CHECK THAT LITCHFIELD PARK IS IN BASE FILE WITH PROPER SAMPLING WEIGHT';
RUN;

PROC FREQ DATA=NIBRS_BASE_FILE;
   TABLES ORI*HIER_OFF;
   TITLE 'COUNTS OF RECORDS IN NIBRS BASE FILE';
RUN;


/* ADJUST COUNTS FOR PARTIAL NIBRS REPORTERS IN SAMPLE TO BE IMPUTED SO WE ONLY IMPUTE
   FOR THE MONTHS AND RECORDS THAT ARE NOT ALREADY IN NIBRS */


DATA IMPUTE_SAMPLE_ADJ (DROP = J PARTIAL_MURDER PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT 
         PARTIAL_BURGLARY PARTIAL_MVTHEFT PARTIAL_LARCENY);
   ARRAY CRIME_TYPE{7} MURDER RAPE ROBBERY AGASSLT BURGLARY MVTHEFT LARCENY;
   ARRAY PARTIAL_CRIME_TYPE{7} PARTIAL_MURDER PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT 
         PARTIAL_BURGLARY PARTIAL_MVTHEFT PARTIAL_LARCENY;
   SET IMPUTE_SAMPLE;
   IF PARTICIPATE IN (1,2) THEN DO;
      DO J = 1 TO 7;
	     CRIME_TYPE{J} = CRIME_TYPE{J} - PARTIAL_CRIME_TYPE{J};
	  END;
   END;
RUN;


PROC PRINT DATA=IMPUTE_SAMPLE;
   WHERE PARTICIPATE IN (1,2);
   TITLE 'LOOK AT ADJUSTED COUNTS FOR PARTIAL REPORTERS';
RUN;

PROC PRINT DATA=IMPUTE_SAMPLE_ADJ;
   WHERE PARTICIPATE IN (1,2);
RUN;

/********* CALCULATE MOS FOR PHOENIX AND TUCSON ****************/
/********* PREPARE DONOR FILE WITH ACS AND VIOLENT CRIME INFORMATION **********/

/* IMPORT FILES WITH ACS INFO */

DATA ACS_DONORS;
   SET NIBRS.ACS_DONORS;
DATA ACS_VARS;
   SET NIBRS.ACS_VARS;
RUN;


/* GET VIOLENT CRIME COUNTS FOR ACS_DONORS */

PROC FREQ DATA=OFFENSEH2021;
   TABLES ORI*HIER_OFF/OUT = CRIME_COUNTS;
RUN;

PROC SORT DATA=CRIME_COUNTS;
   BY ORI;

PROC TRANSPOSE DATA=CRIME_COUNTS OUT = DONOR_LEA_COUNTS;
   BY ORI;
   ID HIER_OFF;
   VAR COUNT;
RUN;

DATA DONOR_LEA_COUNTS (DROP = _NAME_ _LABEL_);
   SET DONOR_LEA_COUNTS;
   YEAR = 2021;
RUN;

/* COMBINE CRIME COUNTS WITH ACS INFO */

PROC SORT DATA=DONOR_LEA_COUNTS;
   BY ORI;
PROC SORT DATA=ACS_DONORS;
   BY ORI;

DATA DONORS_ACS_CRIME;
   MERGE ACS_DONORS (IN = A) DONOR_LEA_COUNTS;
   BY ORI;
   IF A;
   VIOL_CRIME = MURDER + RAPE + ROBBERY + AGASSLT;
   PROP_CRIME = BURGLARY + LARCENY + MVTHEFT;
RUN;


DATA PHX_MOS;
   LENGTH ORI $ 9 MATCHVARS $ 300;
   INFILE DATALINES DSD DELIMITER=',';
   INPUT IMPUTE_NUM MATCHVARS $;
   ORI = "AZ0072300";
   DATALINES;
   1, TOTAL_POP VIOL_CRIME R_COMB_B R_COMB_A
   2, TOTAL_POP VIOL_CRIME R_COMB_W R_COMB_AIAN
   3, TOTAL_POP VIOL_CRIME HISP HS_AND_OVER
   4, TOTAL_POP VIOL_CRIME NH_B R_COMB_AIAN
   5, TOTAL_POP VIOL_CRIME NH_W NO_HEALTH_INS
   6, TOTAL_POP VIOL_CRIME HISP R_COMB_A
   7, TOTAL_POP VIOL_CRIME R_B_ALONE AGE_18_TO_34
   8, TOTAL_POP VIOL_CRIME R_W_ALONE R_COMB_A
   9, TOTAL_POP VIOL_CRIME HISP R_COMB_AIAN
;
RUN;
   
DATA TUCSON_MOS;
   LENGTH ORI $ 9 MATCHVARS $ 300;
   INFILE DATALINES DSD DELIMITER=',';
   INPUT IMPUTE_NUM MATCHVARS $;
   ORI = "AZ0100300";
   DATALINES;
   1, TOTAL_POP VIOL_CRIME R_COMB_B R_COMB_A
   2, TOTAL_POP VIOL_CRIME R_COMB_W R_COMB_AIAN
   3, TOTAL_POP VIOL_CRIME HISP HS_AND_OVER
   4, TOTAL_POP VIOL_CRIME NH_B R_COMB_AIAN
   5, TOTAL_POP VIOL_CRIME NH_W NO_HEALTH_INS
   6, TOTAL_POP VIOL_CRIME HISP R_COMB_A
   7, TOTAL_POP VIOL_CRIME R_B_ALONE AGE_18_TO_34
   8, TOTAL_POP VIOL_CRIME R_W_ALONE R_COMB_A
   9, TOTAL_POP VIOL_CRIME HISP R_COMB_AIAN
;
RUN;


PROC SORT DATA=PHX_MOS;
   BY ORI IMPUTE_NUM;
PROC SORT DATA=ACS_VARS;
   BY ORI;
PROC SORT DATA=AZSAMPLE;
   BY ORI;
DATA PHX_MOS_MERGE;
   MERGE PHX_MOS (IN=A) ACS_VARS AZSAMPLE;
   BY ORI;
   IF A;
   VIOL_CRIME = MURDER + RAPE + ROBBERY + AGASSLT;
RUN;


PROC SORT DATA=TUCSON_MOS;
   BY ORI IMPUTE_NUM;
PROC SORT DATA=ACS_VARS;
   BY ORI;
DATA TUCSON_MOS_MERGE;
   MERGE TUCSON_MOS (IN=A) ACS_VARS AZSAMPLE;
   BY ORI;
   IF A;
   VIOL_CRIME = MURDER + RAPE + ROBBERY + AGASSLT;
RUN;



%CALCULATE_MOS(INDATA=PHX_MOS_MERGE,DONORFILE=DONORS_ACS_CRIME,REL_IMP=3,UPPERBOUND=1,STARTSEED = 34,SOLN = PHX_SOLN,ERROR=PHX_ERROR);

%CALCULATE_MOS(INDATA=TUCSON_MOS_MERGE,DONORFILE=DONORS_ACS_CRIME,REL_IMP=3,UPPERBOUND=1,STARTSEED = 45,SOLN = TUCSON_SOLN,ERROR=TUCSON_ERROR);

PROC PRINT DATA=PHX_SOLN;
TITLE 'PHX MOS';
RUN;
PROC PRINT DATA=PHX_ERROR;
RUN;


PROC PRINT DATA=TUCSON_SOLN;
TITLE 'TUCSON MOS';
RUN;
PROC PRINT DATA=TUCSON_ERROR;
RUN;


PROC SORT DATA=TUCSON_SOLN;
   BY IMPUTE_NUM ORI;
PROC SORT DATA=PHX_SOLN;
   BY IMPUTE_NUM ORI;

DATA MOS_PHX_TUCSON;
   MERGE PHX_SOLN TUCSON_SOLN;
   BY IMPUTE_NUM ORI;
   YEAR = 2021;
RUN;

PROC PRINT DATA=MOS_PHX_TUCSON;
   TITLE 'MOS FOR PHOENIX AND TUCSON';
RUN;

/* LOOK AT VARIABILITY IN MOS_PHX_TUCSON */

PROC MEANS DATA=MOS_PHX_TUCSON MEAN STD MIN MAX CV;
   CLASS ORI;
   TITLE 'VARIABILITY AMONG MOS FOR PHOENIX AND TUCSON';
  RUN;


%LET DSID     = %SYSFUNC(OPEN(WORK.PHX_MOS));
%LET NUMBER_IMPUTATIONS    = %SYSFUNC(ATTRN(&DSID,NLOBS));
%LET RC       = %SYSFUNC(CLOSE(&DSID));
%PUT &NUMBER_IMPUTATIONS;


%MULTIPLE_IMPUTE_MOS(
   LEA_COUNTS     =   IMPUTE_SAMPLE_ADJ 
  ,NUM_IMPUTE     =   &NUMBER_IMPUTATIONS 
  ,MOS_EXTRA      =   MOS_PHX_TUCSON
  ,BASE_DATA      =   NIBRS_BASE_FILE 
  ,DONORSET       =   NIBRS_DONOR /* Input  Name of dataset to be used to obtain incidents for hot deck donors */
  ,SEED_START     =   &SEED1 /* Input  Random number seed to use for starting the imputation */
  ,M_IMPUTE_FILE  =   AZ_2021_IMPUTED
);



/* MERGE THE IMPUTED FILE WITH THE FILE GIVING AGENCY INFORMATION FOR AZ.
   DROP VARIABLES THAT ARE NOT NEEDED FOR COMPUTING CRIME ESTIMATES FROM IMPUTED DATA */


DATA AZ_2021_IMPUTED (DROP = &MOS_ORI_LIST);
   SET AZ_2021_IMPUTED;
RUN;

PROC PRINT DATA= AZ_2021_IMPUTED (OBS=20);
   WHERE _IMPUTATION_ = 1;
   TITLE 'FIRST 20 OBSERVATIONS OF AZ_2021_IMPUTED';
RUN;


/* COMBINE WITH NON-HIERARCHICAL OFFENSE FILE, AND WITH VICTIM, OFFENDER, PROPERTY FILES 
   NEED TO BE CAREFUL WITH MERGING BECAUSE IMPUTED DATAFILE CONTAINS REPEATED INCIDENTS,
   BOTH FROM THE SAMPLING WITH REPLACEMENT AND FROM THE MULTIPLE IMPUTATIONS.
   WANT TO CARRY OVER THE MULTIPLE RECORDS IN THE VICTIM FILE TO EACH INCIDENT RECORD
   IN THE HIERARCHICAL IMPUTED FILE AS MANY TIMES AS IT IS REPEATED --- A MANY-TO-MANY MERGE */

PROC CONTENTS DATA=AZ_2021_IMPUTED;
   TITLE 'CONTENTS OF AZ_2021_IMPUTED';
RUN;


DATA OFFENSEM_MERGE;
   SET OFFENSEM2021 OFFENSEM2022 OFFENSEM2023;
DATA VICTIM_MERGE;
   SET VICTIM2021 VICTIM2022 VICTIM2023;
DATA PROPERTY_MERGE;
   SET PROPERTY2021 PROPERTY2022 PROPERTY2023;
DATA OFFENDER_MERGE;
   SET OFFENDER2021 OFFENDER2022 OFFENDER2023;


   
%MERGE_IMPUTED_FILES(
   HIER_FILE      =   AZ_2021_IMPUTED
  ,MERGE_FILE     =   OFFENSEM_MERGE
  ,MERGED_IMPUTE  =   OFFENSEM_IMPUTED
);

%MERGE_IMPUTED_FILES(
   HIER_FILE      =   AZ_2021_IMPUTED
  ,MERGE_FILE     =   VICTIM_MERGE
  ,MERGED_IMPUTE  =   VICTIM_IMPUTED
);


%MERGE_IMPUTED_FILES(
   HIER_FILE      =   AZ_2021_IMPUTED
  ,MERGE_FILE     =   OFFENDER_MERGE
  ,MERGED_IMPUTE  =   OFFENDER_IMPUTED
);

%MERGE_IMPUTED_FILES(
   HIER_FILE      =   AZ_2021_IMPUTED
  ,MERGE_FILE     =   PROPERTY_MERGE
  ,MERGED_IMPUTE  =   PROPERTY_IMPUTED
);

/* SAVE IMPUTED DATASETS */

DATA NIBRS.OFFENSEH_IMPUTED;
   SET AZ_2021_IMPUTED;
RUN;


DATA NIBRS.OFFENSEM_IMPUTED;
   SET OFFENSEM_IMPUTED;
RUN;

DATA NIBRS.VICTIM_IMPUTED;
   SET VICTIM_IMPUTED;
RUN;


DATA NIBRS.OFFENDER_IMPUTED;
   SET OFFENDER_IMPUTED;
RUN;


DATA NIBRS.PROPERTY_IMPUTED;
   SET PROPERTY_IMPUTED;
RUN;

/* SAVE PHX TUCSON MOS FILE FOR PAPER */


PROC SORT DATA=MOS_PHX_TUCSON;
   BY ORI;

PROC SORT DATA=ACS_DONORS;
   BY ORI;


DATA MOS_PHX_TUCSON_MERGE;
   MERGE MOS_PHX_TUCSON ACS_DONORS (KEEP = ORI PLACE);
   BY ORI;
   CITY = SCAN(PLACE,1,' ');
   IF SUBSTR(PLACE,1,2) in ("Sa","La","El","Fo") THEN CITY = SCAN(PLACE,1,' ')||' '||SCAN(PLACE,2,' ');

PROC SORT DATA=PHX_MOS;
   BY IMPUTE_NUM;
RUN;

PROC SORT DATA=MOS_PHX_TUCSON_MERGE;
   BY IMPUTE_NUM ORI;

DATA MOS_PHX_TUCSON_EXPORT (DROP = ORI YEAR PLACE);
   MERGE PHX_MOS (KEEP = IMPUTE_NUM MATCHVARS ) MOS_PHX_TUCSON_MERGE;
   BY IMPUTE_NUM;

PROC SORT DATA=MOS_PHX_TUCSON_EXPORT;
   BY IMPUTE_NUM CITY;

PROC PRINT DATA=MOS_PHX_TUCSON_EXPORT;
RUN;


PROC EXPORT DATA= WORK.MOS_PHX_TUCSON_EXPORT
            OUTFILE= "&PATH.\Phoenix Tucson MOS.csv" 
            DBMS=CSV REPLACE;
     PUTNAMES=YES;
RUN;

   
