/* Calculate Arizona 2021 UCR crime statistics */

OPTIONS NOFMTERR;

/* Substitute location of computational folder into PATH */
%LET PATH = C:;
%PUT &PATH;

FILENAME AZSAMPLE "&PATH.\AZcounts.csv";

LIBNAME NIBRS "&PATH";

%LET AZ_TOTAL_POP= 7280979;
%PUT &AZ_TOTAL_POP;


DATA STRATSIZE;
   SET NIBRS.STRATSIZE;
RUN;

PROC PRINT DATA=STRATSIZE;
   TITLE 'PRINT STRATUM SIZE AND TOTAL INFORMATION';
RUN;

DATA AZSAMPLE;
   LENGTH ORI $ 9 CITY_NAME $ 30 NAME $ 50;
   INFILE AZSAMPLE DSD DELIMITER = ',' FIRSTOBS = 2;
   INPUT STRATUM ORI $ CITY_NAME $ POP_GROUP $	CURRENT_POPULATION MONTHS_REPORTED PARTICIPATE	NAME $
     MURDER RAPE ROBBERY AGASSLT BURGLARY MVTHEFT LARCENY	
     PARTIAL_MURDER PARTIAL_RAPE PARTIAL_ROBBERY PARTIAL_AGASSLT PARTIAL_BURGLARY PARTIAL_MVTHEFT PARTIAL_LARCENY 
     LEA_WT;
   VIOLCRIME = MURDER + RAPE + ROBBERY + AGASSLT;
   PROPCRIME = BURGLARY + LARCENY + MVTHEFT;
RUN;

PROC SORT DATA=AZSAMPLE;
   BY STRATUM;

PROC SURVEYMEANS DATA=AZSAMPLE TOTAL = STRATSIZE SUM CLSUM PLOTS = NONE;
   WEIGHT LEA_WT;
   STRATA STRATUM;
   VAR VIOLCRIME  MURDER RAPE ROBBERY AGASSLT PROPCRIME  BURGLARY  LARCENY  MVTHEFT;
   DOMAIN STRATUM;
   TITLE 'CRIME COUNT ESTIMATES FOR ARIZONA, 2021';
   ODS OUTPUT STATISTICS = AZCRIMECOUNTS;
RUN;


DATA AZCRIMERATES;
   SET AZCRIMECOUNTS;
   RATE = SUM*100000/&AZ_TOTAL_POP;
   SE_RATE = STDDEV*100000/&AZ_TOTAL_POP;
   RATE_LCL = LOWERCLSUM*100000/&AZ_TOTAL_POP;
   RATE_UCL = UPPERCLSUM*100000/&AZ_TOTAL_POP;
   CV = STDDEV/SUM;
RUN;

PROC PRINT DATA=AZCRIMERATES;
   TITLE 'CRIME RATE ESTIMATES FOR ARIZONA, 2021';
RUN;


PROC EXPORT DATA=AZCRIMERATES
   OUTFILE = "&PATH./AZ crime rates estimated from sample.csv"
   DBMS = csv
   REPLACE;
RUN;

