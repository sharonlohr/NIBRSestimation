
OPTIONS NOFMTERR;
/* PUT WORKING DIRECTORY IN NEXT LINE */
%LET PATH = C:;
%PUT &PATH;

LIBNAME NIBRS "&PATH";

/* READ DATASETS FOR FULL AZ NIBRS REPORTERS */

DATA OFFENSEH_NIBRSREP;
   SET NIBRS.OFFENSEH2021;
   IF NUMSTATE = 2;
RUN;

DATA OFFENSEM_NIBRSREP;
   SET NIBRS.OFFENSEM2021;
   IF NUMSTATE = 2;
RUN;

DATA VICTIM_NIBRSREP;
   SET NIBRS.VICTIM2021; 
   IF NUMSTATE = 2;
RUN;

DATA OFFENDER_NIBRSREP;
   SET NIBRS.OFFENDER2021;
   IF NUMSTATE = 2;
RUN;



/* READ IMPUTED DATASETS AND DATASET GIVING POPULATION TOTALS FOR STRATA */

DATA OFFENSEH_IMPUTED;
   SET NIBRS.OFFENSEH_IMPUTED;
RUN;

DATA OFFENSEM_IMPUTED;
   SET NIBRS.OFFENSEM_IMPUTED;
RUN;

DATA VICTIM_IMPUTED;
   SET NIBRS.VICTIM_IMPUTED; 
RUN;

DATA OFFENDER_IMPUTED;
   SET NIBRS.OFFENDER_IMPUTED;
RUN;

DATA PROPERTY_IMPUTED;
   SET NIBRS.PROPERTY_IMPUTED;
RUN;

DATA STRATSIZE;
   SET NIBRS.STRATSIZE;
RUN;

PROC SQL NOPRINT;
   SELECT COUNT (DISTINCT _IMPUTATION_) INTO :NUM_IMPUTATIONS 
   FROM OFFENSEH_IMPUTED;
QUIT;

%PUT &NUM_IMPUTATIONS ;
%LET WITHIN_DF = 84;
%PUT &WITHIN_DF;

PROC PRINT DATA=STRATSIZE;
RUN;

DATA STRATSIZE_IMP;
   SET STRATSIZE;
   DO _IMPUTATION_ = 1 TO &NUM_IMPUTATIONS;
      OUTPUT;
   END;
RUN;

PROC PRINT DATA=STRATSIZE_IMP;
   TITLE 'STRATUM SIZE FILE WITH IMPUTATION INFORMATION';
RUN;

PROC SORT DATA=STRATSIZE_IMP;
   BY _IMPUTATION_ STRATUM;
RUN;


* ODS SELECT NONE;  /* SUPPRESSES OUTPUT */

/* CHECK OFFENSE COUNT IN HIERARCHICAL FILE, FOR NIBRS REPORTERS */


PROC SURVEYMEANS DATA=OFFENSEH_NIBRSREP SUM  PLOTS = NONE;
   CLASS HIER_OFF;
   TITLE 'HIERARCHICAL CRIME TOTALS FROM 12-MONTH NIBRS REPORTERS';
   VAR VIOL_CRIME PROP_CRIME HIER_OFF;
   ODS OUTPUT STATISTICS = AZ_CRIME_COUNTS_NIBRSREP;
RUN;


PROC PRINT DATA=AZ_CRIME_COUNTS_NIBRSREP; RUN;


PROC SORT DATA=OFFENSEH_IMPUTED;
   BY _IMPUTATION_ STRATUM;
RUN;


PROC SURVEYMEANS DATA = OFFENSEH_IMPUTED TOTAL = STRATSIZE_IMP SUM CLSUM PLOTS = NONE;
   BY _IMPUTATION_;
   CLASS HIER_OFF;
   WEIGHT LEA_WT;
   STRATA STRATUM;
   CLUSTER ORI_SAMPLE;
   VAR VIOL_CRIME PROP_CRIME HIER_OFF;
   TITLE 'CRIME TOTALS FROM IMPUTED DATA';
   ODS OUTPUT STATISTICS = AZ_CRIME_COUNTS;
RUN;

PROC PRINT DATA=AZ_CRIME_COUNTS; RUN;

PROC SORT DATA=AZ_CRIME_COUNTS;
    BY VARNAME VARLEVEL;

PROC MIANALYZE DATA = AZ_CRIME_COUNTS EDF = &WITHIN_DF;
    BY VARNAME VARLEVEL;
    MODELEFFECTS SUM;
	STDERR STDDEV;
	ODS OUTPUT PARAMETERESTIMATES = CRIME_COUNTS_PARAM ;
	TITLE 'ESTIMATE HIERARCHICAL CRIME COUNTS FROM IMPUTED DATA USING MIANALYZE';
RUN;

PROC PRINT DATA=CRIME_COUNTS_PARAM;
RUN;


/* CALCULATE HIERARCHICAL CRIME TOTALS USING FORMULA */


DATA AZ_CRIME_COUNTS;
   SET AZ_CRIME_COUNTS;
   MYSTAT = SUM;
   WITHIN_VARIANCE= STDDEV*STDDEV;
PROC PRINT DATA=AZ_CRIME_COUNTS; RUN;

PROC MEANS DATA=AZ_CRIME_COUNTS MEAN VAR N NOPRINT;
    BY  VARNAME VARLEVEL;
	VAR MYSTAT WITHIN_VARIANCE ;
	OUTPUT OUT = CRIME_COUNTS_HIER_OUT MEAN = POINTEST MEAN_W_VAR VAR = BETWEEN_VAR  N = NUM_IMP;
RUN;

DATA CRIME_COUNTS_HIER_OUT (DROP = _TYPE_ _FREQ_);
   SET CRIME_COUNTS_HIER_OUT;
   STAT_VAR = MEAN_W_VAR + (1+1/NUM_IMP)*BETWEEN_VAR;
   STAT_SE = SQRT(STAT_VAR);
   IF BETWEEN_VAR < 1E-6 THEN DF = &WITHIN_DF;
     ELSE DF = MIN(&WITHIN_DF, (NUM_IMP - 1) * (1 + MEAN_W_VAR/( (1 + 1/NUM_IMP)*BETWEEN_VAR ) )**2 );
   STAT_LCL = POINTEST - STAT_SE*TINV(.975,DF);
   STAT_UCL = POINTEST + STAT_SE*TINV(.975,DF);

PROC PRINT DATA=CRIME_COUNTS_HIER_OUT; RUN;


/* CALCULATE COUNTS FOR ALL OFFENSES (NOT JUST MOST SERIOUS FOR EACH INCIDENT) */


PROC SURVEYMEANS DATA=OFFENSEM_NIBRSREP SUM  PLOTS = NONE;
   CLASS OFFENSE_TYPE;
   TITLE 'ALL (NON-HIERARCHICAL) TOTALS FROM 12-MONTH NIBRS REPORTERS';
   VAR VIOL_OFFENSE PROP_OFFENSE OFFENSE_TYPE;
   ODS OUTPUT STATISTICS = CRIME_COUNTS_NONHIER_NIBRSREP;
RUN;

DATA CRIME_COUNTS_NONHIER_NIBRSREP (KEEP = VARNAME VARLEVEL NIBRS_STAT);
   SET CRIME_COUNTS_NONHIER_NIBRSREP ;
   NIBRS_STAT = SUM;

PROC PRINT DATA = CRIME_COUNTS_NONHIER_NIBRSREP; RUN;

PROC SORT DATA=OFFENSEM_IMPUTED;
   BY _IMPUTATION_ STRATUM;

PROC SURVEYMEANS DATA = OFFENSEM_IMPUTED TOTAL = STRATSIZE_IMP SUM CLSUM MEAN CLM DF PLOTS = NONE;
   BY _IMPUTATION_;
   CLASS OFFENSE_TYPE ;
   WEIGHT LEA_WT;
   STRATA STRATUM;
   CLUSTER ORI_SAMPLE;
   VAR VIOL_OFFENSE PROP_OFFENSE OFFENSE_TYPE;
   TITLE 'CRIME TOTALS FROM IMPUTED DATA, NON-HIERARCHICAL';
   ODS OUTPUT STATISTICS = CRIME_COUNTS_NONHIER;
RUN;

DATA CRIME_COUNTS_NONHIER;
   SET CRIME_COUNTS_NONHIER;
   MYSTAT = SUM;
   WITHIN_VARIANCE= STDDEV*STDDEV;
PROC PRINT DATA=CRIME_COUNTS_NONHIER; RUN;


/* CALCULATE NONHIERARCHICAL CRIME TOTALS USING PROC MIANALYZE */

PROC SORT DATA=CRIME_COUNTS_NONHIER;
    BY  VARNAME VARLEVEL;

PROC MIANALYZE DATA = CRIME_COUNTS_NONHIER EDF = 75;
    BY VARNAME VARLEVEL;
    MODELEFFECTS SUM;
	STDERR STDDEV;
	ODS OUTPUT PARAMETERESTIMATES = NONHIER_CRIME_COUNTS_PARAM ;
	TITLE 'ESTIMATE NONHIERARCHICAL CRIME COUNTS FROM IMPUTED DATA USING MIANALYZE';
RUN;

PROC PRINT DATA=NONHIER_CRIME_COUNTS_PARAM; RUN;

/* CALCULATE NONHIERARCHICAL CRIME TOTALS USING FORMULA */

PROC MEANS DATA=CRIME_COUNTS_NONHIER MEAN VAR N NOPRINT;
    BY  VARNAME VARLEVEL;
	VAR MYSTAT WITHIN_VARIANCE ;
	OUTPUT OUT = CRIME_COUNTS_NONHIER_OUT MEAN = POINTEST MEAN_W_VAR VAR = BETWEEN_VAR  N = NUM_IMP;
RUN;

DATA CRIME_COUNTS_NONHIER_OUT (DROP = _TYPE_ _FREQ_);
   SET CRIME_COUNTS_NONHIER_OUT;
   STAT_VAR = MEAN_W_VAR + (1+1/NUM_IMP)*BETWEEN_VAR;
   STAT_SE = SQRT(STAT_VAR);
   IF BETWEEN_VAR < 1E-6 THEN DF = &WITHIN_DF;
     ELSE DF = MIN(&WITHIN_DF, (NUM_IMP - 1) * (1 + MEAN_W_VAR/( (1 + 1/NUM_IMP)*BETWEEN_VAR ) )**2 );
   STAT_LCL = POINTEST - STAT_SE*TINV(.975,DF);
   STAT_UCL = POINTEST + STAT_SE*TINV(.975,DF);

PROC PRINT DATA=CRIME_COUNTS_NONHIER_OUT; RUN;

PROC SORT DATA=CRIME_COUNTS_NONHIER_OUT;
   BY VARNAME VARLEVEL;
PROC SORT DATA=CRIME_COUNTS_NONHIER_NIBRSREP;
   BY VARNAME VARLEVEL;


DATA CRIME_COUNTS_NONHIER_COMB;
  MERGE CRIME_COUNTS_NONHIER_OUT CRIME_COUNTS_NONHIER_NIBRSREP;
   BY VARNAME VARLEVEL;

PROC PRINT DATA=CRIME_COUNTS_NONHIER_COMB;
RUN;


   /* WEAPON USE FOR VIOLENT OFFENSES */

PROC SURVEYMEANS DATA = OFFENSEM_NIBRSREP MEAN PLOTS = NONE;
   WHERE VIOL_OFFENSE = 1;
   CLASS OFFENSE_TYPE WEAPON_TYPE_KNOWN;
   VAR WEAPON_TYPE_KNOWN;
   DOMAIN OFFENSE_TYPE;
   TITLE 'WEAPON ESTIMATES FROM NIBRS REPORTED, NON-HIERARCHICAL. PERCENTAGE WHEN THERE IS A WEAPON';
   ODS OUTPUT STATISTICS = WEAPON_USE_NIBRSREP DOMAIN = DOMAIN_WEAPON_USE_NIBRSREP;
RUN;


DATA WEAPON_USE_NONHIER_NIBRSREP (KEEP = VARNAME VARLEVEL NIBRS_STAT);
   SET WEAPON_USE_NIBRSREP DOMAIN_WEAPON_USE_NIBRSREP ;
   NIBRS_STAT = MEAN;

PROC PRINT DATA = WEAPON_USE_NONHIER_NIBRSREP; RUN;

DATA WEAPON_USE_NIBRSREP;
   SET WEAPON_USE_NIBRSREP DOMAIN_WEAPON_USE_NIBRSREP;

PROC SURVEYMEANS DATA = OFFENSEM_IMPUTED TOTAL = STRATSIZE_IMP MEAN CLM PLOTS = NONE;
   BY _IMPUTATION_;
   WHERE VIOL_OFFENSE = 1;
   CLASS OFFENSE_TYPE WEAPON_TYPE_KNOWN;
   WEIGHT LEA_WT;
   STRATA STRATUM;
   CLUSTER ORI_SAMPLE;
   VAR WEAPON_TYPE_KNOWN;
   DOMAIN OFFENSE_TYPE;
   TITLE 'WEAPON ESTIMATES FROM IMPUTED DATA, NON-HIERARCHICAL. PERCENTAGE WHEN THERE IS A WEAPON';
   ODS OUTPUT STATISTICS = WEAPON_USE DOMAIN = DOMAIN_WEAPON_USE;
RUN;

DATA WEAPON_USE_TOC;
   SET WEAPON_USE DOMAIN_WEAPON_USE;
RUN;

DATA WEAPON_USE_TOC;
   SET WEAPON_USE_TOC;
   MYSTAT = MEAN;
   WITHIN_VARIANCE= STDERR*STDERR;
RUN;

PROC SORT DATA=WEAPON_USE_TOC;
    BY  VARNAME VARLEVEL OFFENSE_TYPE;

PROC MEANS DATA=WEAPON_USE_TOC MEAN VAR N NOPRINT;
    BY  VARNAME VARLEVEL OFFENSE_TYPE;
	VAR MYSTAT WITHIN_VARIANCE;
	OUTPUT OUT = WEAPON_OUT_TOC MEAN = POINTEST MEAN_W_VAR VAR = BETWEEN_VAR  N = NUM_IMP;
RUN;


DATA WEAPON_OUT_TOC (DROP = _TYPE_ _FREQ_);
   SET WEAPON_OUT_TOC;
   STAT_VAR = MEAN_W_VAR + (1+1/NUM_IMP)*BETWEEN_VAR;
   STAT_SE = SQRT(STAT_VAR);
   IF BETWEEN_VAR < 1E-6 THEN DF = &WITHIN_DF;
     ELSE DF = MIN(&WITHIN_DF, (NUM_IMP - 1) * (1 + MEAN_W_VAR/( (1 + 1/NUM_IMP)*BETWEEN_VAR ) )**2 );
   STAT_LCL = POINTEST - STAT_SE*TINV(.975,DF);
   STAT_UCL = POINTEST + STAT_SE*TINV(.975,DF);

PROC PRINT DATA=WEAPON_OUT_TOC; RUN;

PROC SORT DATA=WEAPON_USE_NONHIER_NIBRSREP;
   BY VARNAME VARLEVEL;
PROC SORT DATA=WEAPON_OUT_TOC;
   BY VARNAME VARLEVEL;

DATA WEAPON_COMB;
   MERGE WEAPON_OUT_TOC WEAPON_USE_NONHIER_NIBRSREP;
   BY VARNAME VARLEVEL;

PROC PRINT DATA=WEAPON_COMB;
RUN;

/* VICTIM DEMOGRAPHICS */



PROC SURVEYMEANS DATA = VICTIM_NIBRSREP MEAN CLM PLOTS = NONE;
   WHERE VIOL_OFFENSE = 1;
   CLASS VICTIM_SEX_KNOWN VICTIM_AGE_GROUP VICTIM_RACE_KNOWN VICTIM_ETH_KNOWN VICTIM_REL_HIER;
   VAR VICTIM_SEX_KNOWN VICTIM_AGE_GROUP VICTIM_AGE_UNDER_18 VICTIM_RACE_KNOWN VICTIM_ETH_KNOWN VICTIM_REL_HIER;
   DOMAIN MURDER RAPE ROBBERY AGASSLT;
   TITLE 'VICTIM CHARACTERISTICS FROM NIBRS REPORTERS, ALL VIOLENT OFFENSES WITH INCIDENTS';
   ODS OUTPUT STATISTICS = VICTIM_STATS_NIBRSREP DOMAIN = DOMAIN_VICTIM_STATS_NIBRSREP;
RUN;

DATA VICTIM_OUT_NIBRSREP (KEEP = VARNAME VARLEVEL DOMAINLABEL NIBRS_STAT);
   SET VICTIM_STATS_NIBRSREP DOMAIN_VICTIM_STATS_NIBRSREP;
   IF MURDER = 0 OR RAPE = 0 OR ROBBERY = 0 OR AGASSLT = 0 THEN DELETE;
   NIBRS_STAT = MEAN;
RUN;

PROC PRINT DATA=VICTIM_OUT_NIBRSREP; RUN;

PROC SORT DATA=VICTIM_IMPUTED;
BY _IMPUTATION_ STRATUM;

PROC SURVEYMEANS DATA = VICTIM_IMPUTED TOTAL = STRATSIZE_IMP MEAN CLM PLOTS = NONE;
   WHERE VIOL_OFFENSE = 1;
   BY _IMPUTATION_;
   CLASS VICTIM_SEX_KNOWN VICTIM_AGE_GROUP VICTIM_RACE_KNOWN VICTIM_ETH_KNOWN VICTIM_REL_HIER;
   WEIGHT LEA_WT;
   STRATA STRATUM;
   CLUSTER ORI_SAMPLE;
   VAR VICTIM_SEX_KNOWN VICTIM_AGE_GROUP VICTIM_AGE_UNDER_18 VICTIM_RACE_KNOWN VICTIM_ETH_KNOWN VICTIM_REL_HIER;
   DOMAIN MURDER RAPE ROBBERY AGASSLT;
   TITLE 'VICTIM CHARACTERISTICS FROM IMPUTED DATA, ALL VIOLENT OFFENSES WITH INCIDENTS';
   ODS OUTPUT STATISTICS = VICTIM_STATS DOMAIN = DOMAIN_VICTIM_STATS;
RUN;

DATA VICTIM_STATS_TOC (DROP = MURDER RAPE ROBBERY AGASSLT);
  SET VICTIM_STATS DOMAIN_VICTIM_STATS;
  IF MURDER = 0 OR RAPE = 0 OR ROBBERY = 0 OR AGASSLT = 0 THEN DELETE;
   MYSTAT = MEAN;
   WITHIN_VARIANCE= STDERR*STDERR;
RUN;


PROC SORT DATA=VICTIM_STATS_TOC;
    BY  VARNAME VARLEVEL DOMAINLABEL;

PROC MEANS DATA=VICTIM_STATS_TOC MEAN VAR N NOPRINT;
    BY  VARNAME VARLEVEL DOMAINLABEL;
	VAR MYSTAT WITHIN_VARIANCE;
	OUTPUT OUT = VICTIM_OUT MEAN = POINTEST MEAN_W_VAR VAR = BETWEEN_VAR  N = NUM_IMP;
RUN;


DATA VICTIM_OUT (DROP = _TYPE_ _FREQ_);
   SET VICTIM_OUT;
   STAT_VAR = MEAN_W_VAR + (1+1/NUM_IMP)*BETWEEN_VAR;
   STAT_SE = SQRT(STAT_VAR);
   IF BETWEEN_VAR < 1E-6 THEN DF = &WITHIN_DF;
     ELSE DF = MIN(&WITHIN_DF, (NUM_IMP - 1) * (1 + MEAN_W_VAR/( (1 + 1/NUM_IMP)*BETWEEN_VAR ) )**2 );
   STAT_LCL = POINTEST - STAT_SE*TINV(.975,DF);
   STAT_UCL = POINTEST + STAT_SE*TINV(.975,DF);
RUN;

PROC SORT DATA=VICTIM_OUT;
   BY VARNAME VARLEVEL DOMAINLABEL;
PROC SORT DATA= VICTIM_OUT_NIBRSREP;
   BY VARNAME VARLEVEL DOMAINLABEL;

DATA VICTIM_DEMOGRAPHICS;
   MERGE VICTIM_OUT VICTIM_OUT_NIBRSREP;
   BY VARNAME VARLEVEL DOMAINLABEL;
   IF NIBRS_STAT >= STAT_LCL AND NIBRS_STAT <= STAT_UCL THEN NIBRS_IN_CI = 1; ELSE NIBRS_IN_CI = 0;

PROC PRINT DATA=VICTIM_DEMOGRAPHICS;
RUN;




/* OFFENDER DEMOGRAPHICS */

PROC SURVEYMEANS DATA = OFFENDER_NIBRSREP  MEAN  PLOTS = NONE;
   WHERE VIOL_CRIME = 1;
   CLASS OFFENDER_SEX_KNOWN OFFENDER_AGE_GROUP OFFENDER_RACE_KNOWN ;
   VAR OFFENDER_SEX_KNOWN OFFENDER_AGE_GROUP OFFENDER_RACE_KNOWN;
   DOMAIN HIER_OFF;
   TITLE 'OFFENDER CHARACTERISTICS FROM IMPUTED DATA, ALL OFFENSES WITH INCIDENTS, NIBRS REPORTERS';
   ODS OUTPUT STATISTICS = OFFENDER_STATS_NIBRSREP DOMAIN = OFFENDER_STATS_NIBRSREP_DOMAIN 
RUN;

DATA OFFENDER_OUT_NIBRSREP (KEEP = VARNAME VARLEVEL DOMAINLABEL NIBRS_STAT HIER_OFF);
   SET OFFENDER_STATS_NIBRSREP OFFENDER_STATS_NIBRSREP_DOMAIN ;
   NIBRS_STAT = MEAN;
RUN;

PROC SORT DATA=OFFENDER_IMPUTED;
   BY _IMPUTATION_ STRATUM;

PROC SURVEYMEANS DATA = OFFENDER_IMPUTED TOTAL = STRATSIZE_IMP MEAN  PLOTS = NONE;
   BY _IMPUTATION_;
   WHERE VIOL_CRIME = 1;
   WEIGHT LEA_WT;
   STRATA STRATUM;
   CLUSTER ORI_SAMPLE;
   CLASS OFFENDER_SEX_KNOWN OFFENDER_AGE_GROUP OFFENDER_RACE_KNOWN ;
   VAR OFFENDER_SEX_KNOWN OFFENDER_AGE_GROUP OFFENDER_RACE_KNOWN;
   DOMAIN HIER_OFF;
   TITLE 'OFFENDER CHARACTERISTICS FROM IMPUTED DATA, ALL OFFENSES WITH INCIDENTS';
   ODS OUTPUT STATISTICS = OFFENDER_STATS DOMAIN = DOMAIN_OFFENDER_STATS;
RUN;

DATA OFFENDER_STATS_TOC;
   SET OFFENDER_STATS DOMAIN_OFFENDER_STATS;
   MYSTAT = MEAN;
   WITHIN_VARIANCE= STDERR*STDERR;
RUN;

PROC SORT DATA=OFFENDER_STATS_TOC;
    BY  VARNAME VARLEVEL DOMAINLABEL HIER_OFF;

PROC MEANS DATA=OFFENDER_STATS_TOC MEAN VAR N NOPRINT;
    BY  VARNAME VARLEVEL DOMAINLABEL HIER_OFF;
	VAR MYSTAT WITHIN_VARIANCE;
	OUTPUT OUT = OFFENDER_OUT MEAN = POINTEST MEAN_W_VAR VAR = BETWEEN_VAR  N = NUM_IMP;
RUN;


DATA OFFENDER_OUT (DROP = _TYPE_ _FREQ_);
   SET OFFENDER_OUT;
   STAT_VAR = MEAN_W_VAR + (1+1/NUM_IMP)*BETWEEN_VAR;
   STAT_SE = SQRT(STAT_VAR);
   IF BETWEEN_VAR < 1E-6 THEN DF = &WITHIN_DF;
     ELSE DF = MIN(&WITHIN_DF, (NUM_IMP - 1) * (1 + MEAN_W_VAR/( (1 + 1/NUM_IMP)*BETWEEN_VAR ) )**2 );
   STAT_LCL = POINTEST - STAT_SE*TINV(.975,DF);
   STAT_UCL = POINTEST + STAT_SE*TINV(.975,DF);
RUN;

PROC PRINT DATA=OFFENDER_OUT; RUN;

PROC SORT DATA=OFFENDER_OUT;
   BY VARNAME VARLEVEL DOMAINLABEL HIER_OFF;
PROC SORT DATA= OFFENDER_OUT_NIBRSREP;
   BY VARNAME VARLEVEL DOMAINLABEL HIER_OFF;
RUN;

DATA OFFENDER_DEMOGRAPHICS;
   MERGE OFFENDER_OUT OFFENDER_OUT_NIBRSREP ;
   BY VARNAME VARLEVEL DOMAINLABEL HIER_OFF;
   IF NIBRS_STAT >= STAT_LCL AND NIBRS_STAT <= STAT_UCL THEN NIBRS_IN_CI = 1; ELSE NIBRS_IN_CI = 0;
RUN;

PROC PRINT DATA=OFFENDER_DEMOGRAPHICS;
RUN;



DATA ALL_STATS (DROP = NUM_IMP);
   SET CRIME_COUNTS_NONHIER_COMB WEAPON_COMB VICTIM_DEMOGRAPHICS OFFENDER_DEMOGRAPHICS;
   IF NIBRS_STAT >= STAT_LCL AND NIBRS_STAT <= STAT_UCL THEN NIBRS_IN_CI = 1; ELSE NIBRS_IN_CI = 0;
RUN;


PROC PRINT DATA=ALL_STATS; RUN;



PROC EXPORT DATA=ALL_STATS
   OUTFILE = "&PATH./AZ statistics from imputed data.xlsx"
   DBMS = xlsx
   REPLACE;
RUN;

/**********DELETE THIS FROM PUBLIC FILE ********************/
/*CREATE OUTPUT DATASET OF IMPUTED RECORDS */

PROC SORT DATA=AZ_CRIME_COUNTS;
   BY VARNAME VARLEVEL _IMPUTATION_;

PROC TRANSPOSE DATA=AZ_CRIME_COUNTS OUT = EXPORT_AZ_CRIME_COUNTS PREFIX = IMP; 
   BY VARNAME VARLEVEL;
   VAR SUM;
   ID _IMPUTATION_;
RUN;

PROC PRINT DATA=EXPORT_AZ_CRIME_COUNTS;
RUN;


PROC EXPORT DATA=EXPORT_AZ_CRIME_COUNTS
   OUTFILE = "&PATH./AZSTATS BY IMPUTATION.xlsx"
   DBMS = xlsx
   REPLACE;
   SHEET = AZ_CRIME_COUNTS;
RUN;


PROC SORT DATA=CRIME_COUNTS_NONHIER;
   BY VARNAME VARLEVEL _IMPUTATION_;

PROC TRANSPOSE DATA=CRIME_COUNTS_NONHIER OUT = EXPORT_CRIME_COUNTS_NONHIER PREFIX = IMP; 
   BY VARNAME VARLEVEL;
   VAR SUM;
   ID _IMPUTATION_;
RUN;

PROC PRINT DATA=EXPORT_CRIME_COUNTS_NONHIER; RUN;


PROC EXPORT DATA=EXPORT_CRIME_COUNTS_NONHIER
   OUTFILE = "&PATH./AZSTATS BY IMPUTATION.xlsx"
   DBMS = xlsx
   REPLACE;
   SHEET = CRIME_COUNTS_NONHIER;
RUN;


PROC SORT DATA=VICTIM_STATS_TOC;
    BY  VARNAME VARLEVEL DOMAINLABEL _IMPUTATION_;

	
PROC TRANSPOSE DATA=VICTIM_STATS_TOC OUT = EXPORT_VICTIM_STATS_TOC PREFIX = IMP; 
   BY VARNAME VARLEVEL DOMAINLABEL;
   VAR MYSTAT;
   ID _IMPUTATION_;


PROC EXPORT DATA=EXPORT_VICTIM_STATS_TOC
   OUTFILE = "&PATH./AZSTATS BY IMPUTATION.xlsx"
   DBMS = xlsx
   REPLACE;
   SHEET = VICTIM;
RUN;


PROC SORT DATA=OFFENDER_STATS_TOC;
    BY  VARNAME VARLEVEL DOMAINLABEL HIER_OFF _IMPUTATION_;

	
PROC TRANSPOSE DATA=OFFENDER_STATS_TOC OUT = EXPORT_OFFENDER_STATS_TOC PREFIX = IMP; 
   BY VARNAME VARLEVEL DOMAINLABEL HIER_OFF;
   VAR MYSTAT;
   ID _IMPUTATION_;


PROC EXPORT DATA=EXPORT_OFFENDER_STATS_TOC
   OUTFILE = "&PATH./AZSTATS BY IMPUTATION.xlsx"
   DBMS = xlsx
   REPLACE;
   SHEET = OFFENDER;
RUN;


PROC SORT DATA=WEAPON_USE_TOC;
    BY  VARNAME VARLEVEL OFFENSE_TYPE;

	
PROC TRANSPOSE DATA=WEAPON_USE_TOC OUT = EXPORT_WEAPON_USE_TOC PREFIX = IMP; 
   BY VARNAME VARLEVEL OFFENSE_TYPE;
   VAR MYSTAT;
   ID _IMPUTATION_;
RUN;

PROC EXPORT DATA=EXPORT_WEAPON_USE_TOC
   OUTFILE = "&PATH./AZSTATS BY IMPUTATION.xlsx"
   DBMS = xlsx
   REPLACE;
   SHEET = WEAPON;
RUN;
